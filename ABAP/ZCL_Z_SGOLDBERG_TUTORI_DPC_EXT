class ZCL_Z_SGOLDBERG_TUTORI_DPC_EXT definition
  public
  inheriting from ZCL_Z_SGOLDBERG_TUTORI_DPC
  create public .

public section.
protected section.

  methods INVOICES_GET_ENTITYSET
    redefinition .
  methods SHIPPERSET_GET_ENTITY
    redefinition .
  methods INVOICES_GET_ENTITY
    redefinition .
private section.
ENDCLASS.



CLASS ZCL_Z_SGOLDBERG_TUTORI_DPC_EXT IMPLEMENTATION.


* <SIGNATURE>---------------------------------------------------------------------------------------+
* | Instance Protected Method ZCL_Z_SGOLDBERG_TUTORI_DPC_EXT->INVOICES_GET_ENTITY
* +-------------------------------------------------------------------------------------------------+
* | [--->] IV_ENTITY_NAME                 TYPE        STRING
* | [--->] IV_ENTITY_SET_NAME             TYPE        STRING
* | [--->] IV_SOURCE_NAME                 TYPE        STRING
* | [--->] IT_KEY_TAB                     TYPE        /IWBEP/T_MGW_NAME_VALUE_PAIR
* | [--->] IO_REQUEST_OBJECT              TYPE REF TO /IWBEP/IF_MGW_REQ_ENTITY(optional)
* | [--->] IO_TECH_REQUEST_CONTEXT        TYPE REF TO /IWBEP/IF_MGW_REQ_ENTITY(optional)
* | [--->] IT_NAVIGATION_PATH             TYPE        /IWBEP/T_MGW_NAVIGATION_PATH
* | [<---] ER_ENTITY                      TYPE        ZCL_Z_SGOLDBERG_TUTORI_MPC=>TS_INVOICE
* | [<---] ES_RESPONSE_CONTEXT            TYPE        /IWBEP/IF_MGW_APPL_SRV_RUNTIME=>TY_S_MGW_RESPONSE_ENTITY_CNTXT
* | [!CX!] /IWBEP/CX_MGW_BUSI_EXCEPTION
* | [!CX!] /IWBEP/CX_MGW_TECH_EXCEPTION
* +--------------------------------------------------------------------------------------</SIGNATURE>
  method INVOICES_GET_ENTITY.

    DATA:
      LV_SHIPPER_NAME TYPE ZJSG_INVOICES-SHIPPER_NAME,
      LV_PRODUCT_NAME TYPE ZJSG_INVOICES-PRODUCT_NAME,
      LV_QUANTITY TYPE ZJSG_INVOICES-QUANTITY.

    LOOP AT IT_KEY_TAB REFERENCE INTO DATA(LV_REF_KEY).
      CASE LV_REF_KEY->NAME.

        WHEN 'ShipperName'.
          LV_SHIPPER_NAME = LV_REF_KEY->VALUE.

        WHEN 'ProductName'.
          LV_PRODUCT_NAME = LV_REF_KEY->VALUE.

        WHEN 'Quantity'.
          LV_QUANTITY = LV_REF_KEY->VALUE.

      ENDCASE.
    ENDLOOP.

    SELECT SINGLE *
    FROM ZJSG_INVOICES
    INTO CORRESPONDING FIELDS OF ER_ENTITY
    WHERE
      SHIPPER_NAME EQ LV_SHIPPER_NAME AND
      PRODUCT_NAME EQ LV_PRODUCT_NAME AND
      QUANTITY EQ LV_QUANTITY.

  endmethod.


* <SIGNATURE>---------------------------------------------------------------------------------------+
* | Instance Protected Method ZCL_Z_SGOLDBERG_TUTORI_DPC_EXT->INVOICES_GET_ENTITYSET
* +-------------------------------------------------------------------------------------------------+
* | [--->] IV_ENTITY_NAME                 TYPE        STRING
* | [--->] IV_ENTITY_SET_NAME             TYPE        STRING
* | [--->] IV_SOURCE_NAME                 TYPE        STRING
* | [--->] IT_FILTER_SELECT_OPTIONS       TYPE        /IWBEP/T_MGW_SELECT_OPTION
* | [--->] IS_PAGING                      TYPE        /IWBEP/S_MGW_PAGING
* | [--->] IT_KEY_TAB                     TYPE        /IWBEP/T_MGW_NAME_VALUE_PAIR
* | [--->] IT_NAVIGATION_PATH             TYPE        /IWBEP/T_MGW_NAVIGATION_PATH
* | [--->] IT_ORDER                       TYPE        /IWBEP/T_MGW_SORTING_ORDER
* | [--->] IV_FILTER_STRING               TYPE        STRING
* | [--->] IV_SEARCH_STRING               TYPE        STRING
* | [--->] IO_TECH_REQUEST_CONTEXT        TYPE REF TO /IWBEP/IF_MGW_REQ_ENTITYSET(optional)
* | [<---] ET_ENTITYSET                   TYPE        ZCL_Z_SGOLDBERG_TUTORI_MPC=>TT_INVOICE
* | [<---] ES_RESPONSE_CONTEXT            TYPE        /IWBEP/IF_MGW_APPL_SRV_RUNTIME=>TY_S_MGW_RESPONSE_CONTEXT
* | [!CX!] /IWBEP/CX_MGW_BUSI_EXCEPTION
* | [!CX!] /IWBEP/CX_MGW_TECH_EXCEPTION
* +--------------------------------------------------------------------------------------</SIGNATURE>
  method INVOICES_GET_ENTITYSET.

    DATA:
          LR_PRODUCT_NAME TYPE RANGE OF ZJSG_INVOICES-PRODUCT_NAME.

    LOOP AT IT_FILTER_SELECT_OPTIONS REFERENCE INTO DATA(LV_REF_FILTER).

      CASE LV_REF_FILTER->PROPERTY.
        WHEN 'ProductName'.
          LOOP AT LV_REF_FILTER->SELECT_OPTIONS REFERENCE INTO DATA(LV_REF_SELECT_OPTIONS).
            APPEND INITIAL LINE TO LR_PRODUCT_NAME REFERENCE INTO DATA(LV_REF_R_PRODUCT_NAME).
            MOVE-CORRESPONDING LV_REF_SELECT_OPTIONS->* TO LV_REF_R_PRODUCT_NAME->*.
          ENDLOOP.
      ENDCASE.

    ENDLOOP.

    LOOP AT IT_ORDER REFERENCE INTO DATA(LV_REF_ORDER).

      CASE LV_REF_ORDER->PROPERTY.
        WHEN 'ShipperName'.
          DATA(LV_DB_FIELD) = 'SHIPPER_NAME'.
      ENDCASE.

      DATA(LV_ORDER) = LV_REF_ORDER->ORDER.

      TRANSLATE LV_ORDER TO UPPER CASE.

      CASE LV_ORDER.
        WHEN 'DESC'.
          DATA(LV_DB_ORDER) = 'DESCENDING'.
        WHEN OTHERS.
          LV_DB_ORDER = 'ASCENDING'.
      ENDCASE.

      CONCATENATE
        LV_DB_FIELD
        LV_DB_ORDER
      INTO
        DATA(LV_ORDER_BY)
      SEPARATED BY
        SPACE.

    ENDLOOP.

    IF IO_TECH_REQUEST_CONTEXT->HAS_COUNT( ) = ABAP_TRUE.

      SELECT COUNT(*)
      FROM ZJSG_INVOICES
      INTO @DATA(LV_COUNT)
      WHERE PRODUCT_NAME IN @LR_PRODUCT_NAME.

      ES_RESPONSE_CONTEXT-COUNT = LV_COUNT.

    ELSE.

      DATA(LV_UP_TO) = IS_PAGING-TOP + IS_PAGING-SKIP.

      SELECT *
      UP TO LV_UP_TO ROWS
      FROM ZJSG_INVOICES
      INTO CORRESPONDING FIELDS OF TABLE ET_ENTITYSET
      WHERE PRODUCT_NAME IN LR_PRODUCT_NAME
      ORDER BY (LV_ORDER_BY).

      IF IS_PAGING-SKIP > 0.
        DELETE ET_ENTITYSET FROM 1 TO IS_PAGING-SKIP.
      ENDIF.

    ENDIF.

  endmethod.


* <SIGNATURE>---------------------------------------------------------------------------------------+
* | Instance Protected Method ZCL_Z_SGOLDBERG_TUTORI_DPC_EXT->SHIPPERSET_GET_ENTITY
* +-------------------------------------------------------------------------------------------------+
* | [--->] IV_ENTITY_NAME                 TYPE        STRING
* | [--->] IV_ENTITY_SET_NAME             TYPE        STRING
* | [--->] IV_SOURCE_NAME                 TYPE        STRING
* | [--->] IT_KEY_TAB                     TYPE        /IWBEP/T_MGW_NAME_VALUE_PAIR
* | [--->] IO_REQUEST_OBJECT              TYPE REF TO /IWBEP/IF_MGW_REQ_ENTITY(optional)
* | [--->] IO_TECH_REQUEST_CONTEXT        TYPE REF TO /IWBEP/IF_MGW_REQ_ENTITY(optional)
* | [--->] IT_NAVIGATION_PATH             TYPE        /IWBEP/T_MGW_NAVIGATION_PATH
* | [<---] ER_ENTITY                      TYPE        ZCL_Z_SGOLDBERG_TUTORI_MPC=>TS_SHIPPER
* | [<---] ES_RESPONSE_CONTEXT            TYPE        /IWBEP/IF_MGW_APPL_SRV_RUNTIME=>TY_S_MGW_RESPONSE_ENTITY_CNTXT
* | [!CX!] /IWBEP/CX_MGW_BUSI_EXCEPTION
* | [!CX!] /IWBEP/CX_MGW_TECH_EXCEPTION
* +--------------------------------------------------------------------------------------</SIGNATURE>
  method SHIPPERSET_GET_ENTITY.

    CASE IV_SOURCE_NAME.

      WHEN 'Invoice'.

        READ TABLE IT_NAVIGATION_PATH
        WITH KEY NAV_PROP = 'shipper'
        TRANSPORTING NO FIELDS.

        IF SY-SUBRC EQ 0.

          READ TABLE IT_KEY_TAB
          WITH KEY NAME = 'ShipperName'
          REFERENCE INTO DATA(LV_REF_KEY).

          SELECT SINGLE *
          FROM ZJSG_SHIPPER
          INTO CORRESPONDING FIELDS OF ER_ENTITY
          WHERE SHIPPER_NAME EQ LV_REF_KEY->VALUE.

          RETURN.

        ENDIF.

    ENDCASE.

    CALL METHOD SUPER->SHIPPERSET_GET_ENTITY
      EXPORTING
        IV_ENTITY_NAME          = IV_ENTITY_NAME
        IV_ENTITY_SET_NAME      = IV_ENTITY_SET_NAME
        IV_SOURCE_NAME          = IV_SOURCE_NAME
        IT_KEY_TAB              = IT_KEY_TAB
        IO_REQUEST_OBJECT       = IO_REQUEST_OBJECT
        IO_TECH_REQUEST_CONTEXT = IO_TECH_REQUEST_CONTEXT
        IT_NAVIGATION_PATH      = IT_NAVIGATION_PATH
      IMPORTING
        ER_ENTITY               = ER_ENTITY
        ES_RESPONSE_CONTEXT     = ES_RESPONSE_CONTEXT
        .

  endmethod.
ENDCLASS.